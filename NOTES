;; Battery Life


batt := batt(), battRemaining := batt.2
MsgBox, 64, Battery status, %battRemaining%`% remaining
MsgBox, 64, Battery status, % batt.2 "% remaining`nStatus: " batt.3 "`nDescription: " batt.4

batt() {
 Static bText := ["Discharging", "AC", "Fully charged", "Low", "Critical", "Charging", "High, charging"
                , "Low, charging", "Critical, charging", "Undefined", "Partially charged"]
 val := []
 For objItem in ComObjGet("winmgmts:").ExecQuery("SELECT * FROM Win32_Battery")
  For each, item in ["BatteryStatus", "EstimatedChargeRemaining", "Status"]
   val.Push(objItem[item]) ; 1 = battery status (number), 2 = percentage remaining, 3 = status (e.g., OK)
 val.Push(bText[val.1])    ; 4 = battery status (text)
 Return val
}
